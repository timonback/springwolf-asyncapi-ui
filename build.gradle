plugins {
  id 'java'
  id 'com.github.node-gradle.node' version '7.0.2'
  id 'maven-publish'
}

def isSnapshot = Boolean.valueOf(project.findProperty('SNAPSHOT'))

group = 'io.github.timonback'
version = '0.1.0' + (isSnapshot ? '-SNAPSHOT' : '')
sourceCompatibility = '1.8'

node {
  version = '18.14.2'
  npmVersion = '9.5.0'
  download = true
}

npm_run_build {
  inputs.file 'package.json'
  inputs.file 'package-lock.json'

  outputs.dir 'build'
}

task buildPages(type: NpmTask) {
  args = ['run', 'build_pages']
}

jar {
  dependsOn 'npm_run_build'
  metaInf { from 'dist' into 'resources/springwolf/asyncapi-ui' }
}

java {
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifactId 'springwolf-asyncapi-ui'
      groupId project.group
      version project.version

      pom {
        name = 'springwolf-asyncapi-ui'
        description = 'AsyncApi UI for Springwolf'
        url = 'https://github.com/timonback/springwolf-asyncapi-ui'

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'timonback'
            name = 'Timon Back'
            email = 'timon.back+springwolf@gmail.com'
          }
        }

        scm {
          url = 'https://github.com/timonback/springwolf-asyncapi-ui'
        }
      }
    }
  }

  repositories {
    maven {
      url = "https://maven.pkg.github.com/timonback/springwolf-asyncapi-ui"
      credentials {
        username = project.findProperty('MAVEN_USERNAME') ?: ''
        password = project.findProperty('MAVEN_PASSWORD') ?: ''
      }
    }
  }
}
